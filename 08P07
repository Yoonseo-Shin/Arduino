// Arduino pin assignment
#define PIN_LED 9
#define PIN_TRIG 12   // sonar sensor TRIGGER
#define PIN_ECHO 13   // sonar sensor ECHO

// configurable parameters
#define SND_VEL 346.0     // sound velocity at 24 celsius degree (unit: m/sec)
#define INTERVAL 25      // sampling interval (unit: msec)
#define PULSE_DURATION 10 // ultra-sound Pulse Duration (unit: usec)
#define _DIST_MIN 100.0   // minimum distance to be measured (unit: mm)
#define _DIST_MAX 300.0   // maximum distance to be measured (unit: mm)

#define TIMEOUT ((INTERVAL / 2) * 1000.0) // maximum echo waiting time (unit: usec)
#define SCALE (0.001 * 0.5 * SND_VEL) // coefficent to convert duration to distance

int light_value; // analogWrite 값. 0~255 사이
float dist_prev = _DIST_MAX; // 이전 거리 측정 값
unsigned long last_sampling_time;   // unit: msec

void setup() {
  // initialize GPIO pins
  pinMode(PIN_LED, OUTPUT);
  pinMode(PIN_TRIG, OUTPUT);  // sonar TRIGGER
  pinMode(PIN_ECHO, INPUT);   // sonar ECHO
  digitalWrite(PIN_TRIG, LOW);  // turn-off Sonar 
  
  // initialize serial port
  Serial.begin(57600);
}

void loop() {
  float distance;

  // wait until next sampling time. 
  // millis() returns the number of milliseconds since the program started.
  //    Will overflow after 50 days.
  if (millis() < (last_sampling_time + INTERVAL))
    return;

  distance = USS_measure(PIN_TRIG, PIN_ECHO); // read distance

  if (distance < _DIST_MIN || distance > _DIST_MAX) { // 만약 측정값이 최저 최고치 넘으면
    distance = dist_prev;    //이전 측정 값을 측정 거리 값 대신 사용.
  }
  
  if (distance<=200 && distance>100) { // 거리가 100~200 사이라면
    light_value = (int)(-255 * distance / 100 + 510); // 밝기 계산 식. 기울기 이용해서 계산함.
    analogWrite(PIN_LED, light_value); // 밝기 제어
    dist_prev = distance; // 이 거리 측정 값이 범위 내에 있는 값이므로 이전 측정 값을 이 값으로 갱신해줌.
  } else if (distance <300 && distance > 200){ // 거리가 200~300 사이라면
    light_value = (int)(255 * distance / 100 - 510); // 밝기 계산 식. 기울기 이용해서 계산함.
    analogWrite(PIN_LED, light_value); 
    dist_prev = distance; // 이 거리 측정 값이 범위 내에 있는 값이므로 이전 측정 값을 이 값으로 갱신해줌.
  } else { // 거리가 범위값을 넘어가면
    analogWrite(PIN_LED, 255); // LED 끄고 이전 측정값 갱신안하기.
  }

  // output the distance to the serial port
  Serial.print("Min:");        Serial.print(_DIST_MIN);
  Serial.print(",distance:");  Serial.print(distance);
  Serial.print(",Max:");       Serial.print(_DIST_MAX);
  Serial.println("");
  
  
  // update last sampling time
  last_sampling_time += INTERVAL;
}

// get a distance reading from USS. return value is in millimeter.
float USS_measure(int TRIG, int ECHO)
{
  digitalWrite(TRIG, HIGH);
  delayMicroseconds(PULSE_DURATION);
  digitalWrite(TRIG, LOW);
  
  return pulseIn(ECHO, HIGH, TIMEOUT) * SCALE; // unit: mm

  // Pulse duration to distance conversion example (target distance = 17.3m)
  // - round trip distance: 34.6m
  // - expected pulse duration: 0.1 sec, or 100,000us
  // - pulseIn(ECHO, HIGH, timeout) * 0.001 * 0.5 * SND_VEL
  //        = 100,000 micro*sec * 0.001 milli/micro * 0.5 * 346 meter/sec
  //        = 100,000 * 0.001 * 0.5 * 346 * micro * sec * milli * meter
  //                                        ----------------------------
  //                                         micro * sec
  //        = 100 * 173 milli*meter = 17,300 mm = 17.3m
  // pulseIn() returns microseconds.
}
